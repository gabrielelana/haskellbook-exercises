# -*- eval: (org-babel-lob-ingest "./ob-haskell-common.org"); -*-

#+TITLE: Monad Transformers

#+PROPERTY: header-args:haskell :results replace output
#+PROPERTY: header-args:haskell+ :noweb yes
#+PROPERTY: header-args:haskell+ :wrap EXAMPLE

* 26.01 Monad Transformers
...

* 26.02 MaybeT
Note that we need to define ~Functor~ and ~Applicative~ instances for
~MaybeT~ but we don't need to do anything specific with ~Maybe~ aka
all ~Functor~ and ~Applicative~ instances of transformers will look
the same (XXX: is this true?).

#+NAME: MaybeT
#+BEGIN_SRC haskell :results silent
import Control.Applicative

newtype MaybeT m a = MaybeT { runMaybeT :: m (Maybe a) }

:{
instance Functor m => Functor (MaybeT m) where
  fmap f (MaybeT ma) = MaybeT $ (fmap . fmap) f ma
:}

:{
instance Applicative m => Applicative (MaybeT m) where
  pure = MaybeT . pure . pure

  (MaybeT mf) <*> (MaybeT ma) = MaybeT $ liftA2 (<*>) mf ma
:}

:{
instance Monad m => Monad (MaybeT m) where
  return = pure

  -- ma :: MaybeT m a
  -- f :: a -> MaybeT m b
  (MaybeT ma) >>= f = MaybeT $ do
    -- ma :: m (Maybe a)
    -- v :: Maybe a
    v <- ma
    case v of
      Nothing -> return Nothing
      Just a -> (runMaybeT . f) a
:}
#+END_SRC

* 26.3 EitherT
(XXX: why the order ~e m a~ for the type parameters and not ~m e a~?)

#+NAME: EitherT
#+BEGIN_SRC haskell :results silent
import Control.Applicative
import Data.Functor

:set -XLambdaCase

newtype EitherT e m a = EitherT { runEitherT :: m (Either e a) }

-- 1. Write the Functor instance for EitherT
:{
instance Functor m => Functor (EitherT e m) where
  fmap f (EitherT ma) = EitherT $ (fmap . fmap) f ma
:}

-- 2. Write the Applicative instance for EitherT
:{
instance Applicative m => Applicative (EitherT e m) where
  pure = EitherT . pure . pure

  (EitherT mf) <*> (EitherT ma) = EitherT $ liftA2 (<*>) mf ma
  -- (EitherT mf) <*> (EitherT ma) = EitherT $ (<*>) <$> mf <*> ma
:}

-- 3. Write the Monad instance for EitherT
:{
instance Monad m => Monad (EitherT e m) where
  return = pure

  (EitherT ma) >>= f = EitherT $ do
    v <- ma
    case v of
      Left e -> return $ Left e
      Right a -> (runEitherT . f) a
:}

-- 4. Write the swapEitherT helper function for EitherT
:{
swapEitherT :: (Functor m) => EitherT e m a -> EitherT a m e
swapEitherT (EitherT ma) = EitherT $ swapEither <$> ma
-- without swapEither
-- swapEitherT (EitherT ma) = EitherT $ ma <&> \case Left e -> Right e
--                                                   Right a -> Left a
:}

-- 5. Write the transformer variant of either catamorphism
:{
eitherT :: (Monad m) => (a -> m c) -> (b -> m c) -> EitherT a m b -> m c
eitherT f g (EitherT ma) = ma >>= (either f g)
-- without either
-- eitherT f g (EitherT ma) = do
--   v <- ma
--   case v of
--     Left a -> f a
--     Right b -> g b
:}
#+END_SRC

* 26.4 ReaderT
#+NAME: ReaderT
#+BEGIN_SRC haskell :results silent
newtype ReaderT r m a = ReaderT { runReaderT :: r -> m a }

:{
instance Functor m => Functor (ReaderT r m) where
  fmap f (ReaderT fma) = ReaderT $ \r -> f <$> (fma r)
:}

:{
instance Applicative m => Applicative (ReaderT r m) where
  pure = ReaderT . const . pure

  (ReaderT fmf) <*> (ReaderT fma) = ReaderT $ \r -> (fmf r) <*> (fma r)
:}

:{
instance Monad m => Monad (ReaderT r m) where
  return = pure

  (ReaderT fma) >>= f = ReaderT $ \r -> fma r >>= flip (runReaderT . f) r
:}
#+END_SRC

* 26.5 StateT
#+NAME: StateT
#+BEGIN_SRC haskell :results silent
import Control.Applicative
import Data.Bifunctor

:set -XTupleSections

newtype StateT s m a = StateT { runStateT :: s -> m (a, s) }

:{
instance Functor m => Functor (StateT s m) where
  fmap f (StateT sma) = StateT $ \s -> first f <$> sma s
:}

:{
instance Monad m => Applicative (StateT s m) where
  pure a = StateT $ pure . (,) a

  -- Wrong, without properly sequencing the state
  -- (StateT smf) <*> (StateT sma) =
  --   StateT $ \s -> ((first . fst) <$> (smf s)) <*> (sma s)

  -- Right, but I don't know why
  -- (StateT smf) <*> (StateT sma) = StateT $ do
  --   mf <- smf
  --   ma <- sma
  --   return $ (first . fst) <$> mf <*> ma

  (StateT smf) <*> (StateT sma) = StateT $ \s -> do
    (f, s') <- smf s
    (a, s'') <- sma s'
    return $ (f a, s'')
:}

:{
instance Monad m => Monad (StateT s m) where
  return = pure

  (StateT sma) >>= f = StateT $ \s -> do
    (a, s') <- sma s
    runStateT (f a) s'
:}
#+END_SRC

* 26.6 Types You Probably Don't Want to Use
...

* 26.7 An Ordinary Type from a Transformer
If you have a transformer variant of a type and want to use it as a
non transformative then you need a structure ~Monad m => m~ that
doesn't do anything... ~Identity~

* 26.8 Lexically Inner is Structurally Outer
In the following transformative types

#+BEGIN_SRC haskell :results silent
newtype EitherT e m a = EitherT { runEitherT :: m (Either e a) }
newtype MaybeT m a = MaybeT { runMaybeT :: m (Maybe a) }
newtype ReaderT r m a = ReaderT { runReaderT :: r -> m a }
#+END_SRC

The structure ~m~ is lexically internal to the type but the structure
is applied externally, around our value.

The structure ~m~ is only wrapped around to things we can /have/ not
to something we /need/ (look at ~ReaderT~)

When an Haskeller say /base monad/ they usually mean what is
structurally outermost.

#+BEGIN_SRC haskell
<<MaybeT>>
<<EitherT>>
<<ReaderT>>

:{
embedded :: MaybeT
            (EitherT String
                     (ReaderT () IO))
            Int
embedded = return 1
:}

:{
maybeUnwrap :: EitherT String
                       (ReaderT () IO)
                       (Maybe Int)
maybeUnwrap = runMaybeT embedded
:}

:{
eitherUnwrap :: ReaderT ()
                        IO
                        (Either String (Maybe Int))
eitherUnwrap = runEitherT maybeUnwrap
:}

:{
readerUnwrap :: () -> IO (Either String (Maybe Int))
readerUnwrap = runReaderT eitherUnwrap
:}

readerUnwrap ()
#+END_SRC

#+RESULTS:
#+BEGIN_EXAMPLE
Right (Just 1)
#+END_EXAMPLE


** Exercise
#+BEGIN_SRC haskell :results silent
<<MaybeT>>
<<EitherT>>
<<ReaderT>>

:{
embedded :: MaybeT
            (EitherT String
                     (ReaderT () IO))
            Int
embedded = MaybeT $ EitherT $ ReaderT $ const $ return $ Right $ Just 1
:}
#+END_SRC

* 26.9 MonadTrans
We already know how to lift a function to work with some
structure. Here we want to lift a function to reach some structurally
outermost position of a monad transformer stack.

** The Typeclass that Lifts
There's a typeclass ~MonadTrans~ with a method ~lift~ that lifts
actions in some ~Monad~ over a transformer type.

#+BEGIN_SRC haskell
import Control.Monad.Trans.Class

:i MonadTrans
#+END_SRC

#+RESULTS:
#+BEGIN_EXAMPLE
class MonadTrans (t :: (* -> *) -> * -> *) where
  lift :: Monad m => m a -> t m a
  {-# MINIMAL lift #-}
  	-- Defined in ‘Control.Monad.Trans.Class’
#+END_EXAMPLE

** Motivating MonadTrans
TODO: scotty example

** MonadTrans Instances
#+NAME: IdentityT
#+BEGIN_SRC haskell :results silent
newtype IdentityT m a = IdentityT { runIdentityT :: m a }

:{
instance Functor m => Functor (IdentityT m) where
  fmap f (IdentityT ma) = IdentityT $ fmap f ma
:}

:{
instance Applicative m => Applicative (IdentityT m) where
  pure = IdentityT . pure

  (IdentityT fmf) <*> (IdentityT fma) = IdentityT $ fmf <*> fma
:}

:{
instance Monad m => Monad (IdentityT m) where
  return = pure

  (IdentityT ma) >>= f = IdentityT $ ma >>= (runIdentityT . f)
:}
#+END_SRC

#+BEGIN_SRC haskell :results silent
<<IdentityT>>

import Control.Monad.Trans.Class

:{
instance MonadTrans IdentityT where
  lift = IdentityT
:}
#+END_SRC

#+NAME: MaybeTR
#+BEGIN_SRC haskell :results silent
<<MaybeT>>

import Control.Monad.Trans.Class
import Control.Monad

:{
instance MonadTrans MaybeT where
  lift = MaybeT . liftM Just
:}
#+END_SRC

#+NAME: ReaderTR
#+BEGIN_SRC haskell :results silent
<<ReaderT>>

import Control.Monad.Trans.Class

:{
instance MonadTrans (ReaderT r) where
  lift = ReaderT . const
:}
#+END_SRC

** Exercises: Lift More

1. Define ~MonadTrans~ instance of ~EitherT~
   #+NAME: EitherTR
   #+BEGIN_SRC haskell :results silent
   <<EitherT>>

   import Control.Monad.Trans.Class
   import Control.Monad

   :{
   instance MonadTrans (EitherT e) where
     lift = EitherT . (liftM Right)
   :}
   #+END_SRC

2. Define ~MonadTrans~ instance of ~StateT~
   #+NAME: StateTR
   #+BEGIN_SRC haskell :results silent
   <<StateT>>

   :set -XTupleSections

   import Control.Monad.Trans.Class
   import Control.Monad

   :{
   instance MonadTrans (StateT s) where
     lift ma = StateT $ \s -> liftM (,s) $ ma
   :}
   #+END_SRC

** Prolific Lifting is the Failure Mode
...

** Wrap it, Smack it, Pre-Lift it
Do not expose the full stack of your monads, only expose what's
useful, wrap everything in a ~newtype~ and implement all the
instances.

#+BEGIN_SRC haskell
import Web.Scotty
import Data.Text.Lazy

:i ActionM

import Web.Scotty.Internal.Types

:i ActionT
#+END_SRC

#+RESULTS:
#+BEGIN_EXAMPLE
type ActionM = Web.Scotty.Internal.Types.ActionT Text IO :: * -> *
  	-- Defined in ‘Web.Scotty’
type role ActionT nominal representational nominal
newtype ActionT e (m :: * -> *) a
  = ActionT {runAM :: Control.Monad.Trans.Except.ExceptT
                        (ActionError e)
                        (Control.Monad.Trans.Reader.ReaderT
                           ActionEnv
                           (Control.Monad.Trans.State.Strict.StateT ScottyResponse m))
                        a}
  	-- Defined in ‘Web.Scotty.Internal.Types’
instance (Monad m, ScottyError e) => Alternative (ActionT e m)
  -- Defined in ‘Web.Scotty.Internal.Types’
instance Monad m => Applicative (ActionT e m)
  -- Defined in ‘Web.Scotty.Internal.Types’
instance Functor m => Functor (ActionT e m)
  -- Defined in ‘Web.Scotty.Internal.Types’
instance (Monad m, ScottyError e) => Monad (ActionT e m)
  -- Defined in ‘Web.Scotty.Internal.Types’
instance (Monad m, ScottyError e) => MonadPlus (ActionT e m)
  -- Defined in ‘Web.Scotty.Internal.Types’
instance (Monad m, ScottyError e, Monoid a) =>
         Monoid (ActionT e m a)
  -- Defined in ‘Web.Scotty.Internal.Types’
instance (Monad m, Semigroup a) => Semigroup (ActionT e m a)
  -- Defined in ‘Web.Scotty.Internal.Types’
instance (Monad m, ScottyError e) => MonadFail (ActionT e m)
  -- Defined in ‘Web.Scotty.Internal.Types’
instance MonadTrans (ActionT e)
  -- Defined in ‘Web.Scotty.Internal.Types’
#+END_EXAMPLE

* 26.10 MonadIO
Rather than lifting through one layer at a time, ~MonadIO~ is intended
to keep lifting until it is lifted over all structure embedded in the
outermost ~IO~ type.

#+BEGIN_SRC haskell
import Control.Monad.IO.Class

:i MonadIO
#+END_SRC

#+RESULTS:
#+BEGIN_EXAMPLE
class Monad m => MonadIO (m :: * -> *) where
  liftIO :: IO a -> m a
  {-# MINIMAL liftIO #-}
  	-- Defined in ‘Control.Monad.IO.Class’
instance [safe] MonadIO IO -- Defined in ‘Control.Monad.IO.Class’
instance (MonadIO m, ScottyError e) => MonadIO (ActionT e m)
  -- Defined in ‘Web.Scotty.Internal.Types’
#+END_EXAMPLE

Instance of ~IdentityT~
#+BEGIN_SRC haskell :results silent
<<IdentityT>>

import Control.Monad.IO.Class

:{
instance (MonadIO m) => MonadIO (IdentityT m) where
  liftIO = IdentityT . liftIO
:}
#+END_SRC

#+BEGIN_SRC haskell :results silent
<<EitherTR>>

import Control.Monad.IO.Class

:{
instance (MonadIO m) => MonadIO (EitherT e m) where
  liftIO = lift . liftIO
:}
#+END_SRC

1. Define ~LiftIO~ instance of ~MaybeT~
   #+BEGIN_SRC haskell :results silent
   <<MaybeTR>>

   import Control.Monad.IO.Class

   :{
   instance (MonadIO m) => MonadIO (MaybeT m) where
     liftIO = lift . liftIO
   :}
   #+END_SRC

2. Define ~LiftIO~ instance of ~ReaderT~
   #+BEGIN_SRC haskell :results silent
   <<ReaderTR>>

   import Control.Monad.IO.Class

   :{
   instance (MonadIO m) => MonadIO (ReaderT r m) where
     liftIO = lift . liftIO
   :}
   #+END_SRC

3. Define ~LiftIO~ instance of ~StateT~
   #+BEGIN_SRC haskell :results silent
   <<StateTR>>

   import Control.Monad.IO.Class

   :{
   instance (MonadIO m) => MonadIO (StateT s m) where
     liftIO = lift . liftIO
   :}
   #+END_SRC

* 26.11 Monad Transformers in Use
...

* 26.12 Monads do not Commute

XXX: Why ~x~ doesn't compile?
#+BEGIN_SRC haskell
<<ReaderT>>
<<MaybeT>>

import Data.Functor.Identity

type Reader r a = ReaderT r Identity a

:{
x :: MaybeT (Reader Int) Int
x = undefined
:}

:{
y :: ReaderT Int Maybe Int
y = undefined
:}
#+END_SRC

#+RESULTS:
#+BEGIN_EXAMPLE
<interactive>:644:6: error:
    • The type synonym ‘Reader’ should have 2 arguments, but has been given 1
    • In the type signature: x :: MaybeT (Reader Int) Int
#+END_EXAMPLE

* 26.13 Transform if you Want To
...

* 26.14 Chapter Exercises
1. Implement ~rDec~
   #+BEGIN_SRC haskell
   import Control.Monad.Trans.Reader
   import Data.Functor.Identity

   :{
   rDec :: Num a => Reader a a
   rDec = ReaderT $ \a -> Identity $ a - 1
   :}

   runReader rDec 1

   fmap (runReader rDec) [1..10]
   #+END_SRC

   #+RESULTS:
   #+BEGIN_EXAMPLE
   0
   [0,1,2,3,4,5,6,7,8,9]
   #+END_EXAMPLE

2. Make ~rDec~ point free
   #+BEGIN_SRC haskell
   import Control.Monad.Trans.Reader
   import Data.Functor.Identity

   :{
   rDec :: Num a => Reader a a
   rDec = ReaderT $ (-) 1 <$> Identity
   :}

   runReader rDec 1
   #+END_SRC

   #+RESULTS:
   #+BEGIN_EXAMPLE
   0
   #+END_EXAMPLE

3. Implement ~rShow~ which is ~show~ but in ~Reader~
   #+BEGIN_SRC haskell
   :{
   rShow :: Show a => ReaderT a Identity String
   rShow = ReaderT $ \r -> Identity $ show r
   :}

   runReaderT rShow 1
   runIdentity $ runReaderT rShow 1
   #+END_SRC

   #+RESULTS:
   #+BEGIN_EXAMPLE
   Identity "1"
   1
   #+END_EXAMPLE

4. Make ~rShow~ point free
   #+BEGIN_SRC haskell
   :{
   rShow :: Show a => ReaderT a Identity String
   -- rShow = show <$> ask
   rShow = ReaderT $ Identity <$> show
   :}

   runReaderT rShow 1
   #+END_SRC

   #+RESULTS:
   #+BEGIN_EXAMPLE
   <interactive>:786:9: error:
       • Couldn't match type ‘Control.Monad.Trans.Reader.ReaderT () m0’
                        with ‘ReaderT a Identity’
         NB: ‘ReaderT’ is defined at <interactive>:653:1-58
             ‘Control.Monad.Trans.Reader.ReaderT’
               is defined in ‘Control.Monad.Trans.Reader’
                   in package ‘transformers-0.5.6.2’
         Expected type: ReaderT a Identity String
           Actual type: Control.Monad.Trans.Reader.ReaderT () m0 String
       • In the expression: show <$> ask
         In an equation for ‘rShow’: rShow = show <$> ask
       • Relevant bindings include
           rShow :: ReaderT a Identity String (bound at <interactive>:786:1)
   Identity "1"
   #+END_EXAMPLE

5. Implement ~rPrintAndInt~ will first print the input with a
   greeting, then return the input incremented by one
   #+BEGIN_SRC haskell
   :{
   rPrintAndInc :: (Num a, Show a) => ReaderT a IO a
   rPrintAndInc = ReaderT $ \a -> putStrLn ("Hi: " <> show a) >> (return $ a + 1)
   :}

   runReaderT rPrintAndInc 1
   traverse (runReaderT rPrintAndInc) [1..10]
   #+END_SRC

   #+RESULTS:
   #+BEGIN_EXAMPLE
   Hi: 1
   2
   Hi: 1
   Hi: 2
   Hi: 3
   Hi: 4
   Hi: 5
   Hi: 6
   Hi: 7
   Hi: 8
   Hi: 9
   Hi: 10
   [2,3,4,5,6,7,8,9,10,11]
   #+END_EXAMPLE

6. Implement ~sPrintIntAccum~ first print the input with a greeting,
   then puts the incremented input as the new state and returns the
   original as a String.

   #+BEGIN_SRC haskell
   import Control.Monad.Trans.State

   :{
   sPrintIntAccum :: (Num a, Show a) => StateT a IO String
   sPrintIntAccum = StateT $
                    \a -> let s = show a in
                            putStrLn ("Hi: " <> s) >> return (s, a + 1)
   :}

   runStateT sPrintIntAccum 10

   mapM (runStateT sPrintIntAccum) [1..5]
   #+END_SRC

   #+RESULTS:
   #+BEGIN_EXAMPLE
   Hi: 10
   ("10",11)
   Hi: 1
   Hi: 2
   Hi: 3
   Hi: 4
   Hi: 5
   [("1",2),("2",3),("3",4),("4",5),("5",6)]
   #+END_EXAMPLE

** Fix the Code
#+BEGIN_SRC haskell :results silent
import Control.Monad.Trans.Maybe
import Control.Monad

:{
isValid :: String -> Bool
isValid v = '!' `elem` v
:}

:{
maybeExcite :: MaybeT IO String
maybeExcite = do
  v <- liftIO getLine
  guard $ isValid v
  return v
:}

:{
doExcite :: IO ()
doExcite = do
  putStrLn "say something excite!"
  excite <- runMaybeT maybeExcite

  case excite of
    Nothing -> putStrLn "MOAR EXCITE"
    Just e -> putStrLn $ "Good " ++ e
:}
#+END_SRC

** Hit Counter
[[file:chapter-026/Counter.hs::{-# LANGUAGE OverloadedStrings #-}][See implementation]]

** Morra
[[file:chapter-026/Morra.hs::module Morra where][See implementation]]
